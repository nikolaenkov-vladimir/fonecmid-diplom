&НаСервере

Функция ВыполнитьФоновоеЗаданиеНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ВКМ_МассовоеСозданиеАктов.ЗаполнитьНаСервере", 
	Объект.Период.ДатаНачала, Объект.Период.ДатаОкончания);
	
КонецФункции




&НаКлиенте
Процедура Заполнить(Команда)
	Если НЕ ЗначениеЗаполнено(Объект.Период) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Период не выбран");
		
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ВыполнитьФоновоеЗаданиеНаСервере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьДанные", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДанные(Результат, ДополнительныеПараметры) Экспорт
	
		Если Результат = Неопределено Тогда
			Возврат;
			
		ИначеЕсли Результат.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
			Возврат;
		ИначеЕсли Результат.Статус = "Выполнено" Тогда
			
			СписокРеализацийМассив = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
			ЗаполнитьОбработкуАктамиЗаПериод(СписокРеализацийМассив);
			
		КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбработкуАктамиЗаПериод(СписокРеализацийМассив)
	
	Объект.ДокументыЗаПериод.Очистить();
	
	Для Каждого СписокРеализацияСтруктура Из СписокРеализацийМассив Цикл
		
		НоваяСтрока = Объект.ДокументыЗаПериод.Добавить();
		НоваяСтрока.Договор = СписокРеализацияСтруктура.Договор;
		НоваяСтрока.ДокументРеализация = СписокРеализацияСтруктура.Реализация;
		
	КонецЦикла;
	
КонецПроцедуры

